buildscript {
  repositories {
    ivy {
      url "file://${project.projectDir}/syncRepo"
    }
    if(!System.getenv('RESTRICTED')) {
      mavenCentral()
      maven {
        url "http://repo.spring.io/libs-release"
      }
      maven {
        url "https://plugins.gradle.org/m2/"
      }
    }
  }
  dependencies {
    classpath("org.springframework.boot:spring-boot-gradle-plugin:1.3.5.RELEASE")
    classpath "io.spring.gradle:dependency-management-plugin:0.6.0.RELEASE"
    classpath "org.ysb33r.gradle:ivypot:0.4"
  }
}

apply plugin: 'org.ysb33r.ivypot'
apply plugin: "java"
apply plugin: "war"
apply plugin: "spring-boot"

def springBootVersion="1.3.5.RELEASE";

dependencies {
    compile "org.springframework.boot:spring-boot-starter-web:${springBootVersion}"
}

repositories {
  if(!System.getenv('RESTRICTED')) {
    mavenCentral()
    maven {
      url "http://repo.spring.io/libs-release"
    }
  }
  ivy {
    url "file://${project.projectDir}/syncRepo"
  }
}

syncRemoteRepositories {
  repoRoot "${project.projectDir}/syncRepo"
  repositories {
    mavenCentral()
    maven {
      url "http://repo.spring.io/libs-release"
    }
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }
  includeBuildScriptDependencies = true
}
